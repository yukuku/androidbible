apply plugin: 'com.android.application'

apply plugin: 'me.tatarka.retrolambda'

android {
    signingConfigs {
        release {
            keyAlias System.getenv("SIGN_ALIAS")
            keyPassword System.getenv("SIGN_PASSWORD")
            storeFile file(System.getenv("SIGN_KEYSTORE") != null ? System.getenv("SIGN_KEYSTORE") : "/dev/null")
            storePassword System.getenv("SIGN_PASSWORD")
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId 'yuku.alkitab.debug'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 14000265
        versionName '4.4.0-beta5'
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
    }
    productFlavors {
        // Use this for development
        emulator_v21 {
            minSdkVersion 21
        }

        // Use this for development testing on Android < 5.0
        plain {
        }

        // The following flavors are for release
        yuku_alkitab {
            applicationId 'yuku.alkitab'
        }
        yuku_quick_bible {
            applicationId 'yuku.alkitab.kjv'
        }
        sabda_alkitab {
            applicationId 'org.sabda.alkitab'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':AlkitabFeedback')
    compile project(':AlkitabIntegration')
    compile project(':AlkitabIo')
    compile project(':AlkitabModel')
    compile project(':AlkitabYes2')
    compile project(':BiblePlus')
    compile project(':DragSortListView')
    compile project(':KpriModel')
    compile project(':BintexReader')
    compile project(':BintexWriter')
    compile project(':Afw')
    compile project(':FlowLayout')
    compile project(':AmbilWarna')
    compile project(':ATree')
    compile project(':ImportedDesktopVerseUtil')
    compile('com.afollestad.material-dialogs:core:0.8.5.2@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.8.5.2@aar') {
        transitive = true
    }
    testCompile 'junit:junit:4.12'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.2.0'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.google.android.gms:play-services-gcm:8.3.0'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:percent:23.1.1'
    compile 'com.android.support:preference-v14:23.1.1'
}
