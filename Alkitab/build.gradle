buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.31.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    signingConfigs {
        release {
            keyAlias System.getenv("SIGN_ALIAS")
            keyPassword System.getenv("SIGN_PASSWORD")
            storeFile file(System.getenv("SIGN_KEYSTORE") != null ? System.getenv("SIGN_KEYSTORE") : "/dev/null")
            storePassword System.getenv("SIGN_PASSWORD")
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId 'yuku.alkitab.debug'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 17000420
        versionName '4.5.7'
        multiDexEnabled true
        // Keep this synced with integrate_translations.sh!
        resConfigs 'af', 'bg', 'ceb', 'cs', 'da', 'de', 'es', 'fr', 'in', 'it', 'ja', 'ko', 'lv', 'ms', 'nl', 'pl', 'pt', 'ro', 'ru', 'th', 'tl', 'tr', 'vi', 'zh-rCN', 'zh-rTW'
        buildConfigField 'String', 'SERVER_HOST', "\"$SERVER_HOST\""
        buildConfigField 'String', 'RIBKA_FUNCTIONS_HOST', "\"$RIBKA_FUNCTIONS_HOST\""
    }
    buildTypes {
        debug {
            buildConfigField 'String', 'RIBKA_FUNCTIONS_HOST', "\"$RIBKA_FUNCTIONS_HOST_DEBUG\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dexOptions {
        javaMaxHeapSize "2g"
        // Skip pre-dexing when running on Travis CI
        preDexLibraries = !travisBuild
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }

    flavorDimensions "playStoreApplicationId"

    productFlavors {
        // Use this for development
        plain {
        }

        // The following flavors are for release
        yuku_alkitab {
            applicationId 'yuku.alkitab'
        }
        yuku_quick_bible {
            applicationId 'yuku.alkitab.kjv'
        }
        sabda_alkitab {
            applicationId 'org.sabda.alkitab'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':AlkitabFeedback')
    implementation project(':AlkitabIntegration')
    implementation project(':AlkitabIo')
    implementation project(':AlkitabModel')
    implementation project(':AlkitabYes2')
    implementation project(':BiblePlus')
    implementation project(':DragSortListView')
    implementation project(':KpriModel')
    implementation project(':BintexReader')
    implementation project(':BintexWriter')
    implementation project(':Afw')
    implementation project(':FlowLayout')
    implementation project(':AmbilWarna')
    implementation project(':ATree')
    implementation project(':ImportedDesktopVerseUtil')
    implementation project(':PrDownloaderFixed')
    implementation "com.afollestad.material-dialogs:core:$materialDialogsVersion"
    implementation "com.afollestad.material-dialogs:commons:$materialDialogsVersion"
    testImplementation 'junit:junit:4.12'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    debugImplementation "com.facebook.stetho:stetho:$stethoVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.takisoft.fix:preference-v7:$preferenceFixVersion"
    implementation "com.google.firebase:firebase-messaging:17.5.0"
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
